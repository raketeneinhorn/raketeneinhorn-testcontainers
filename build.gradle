import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java-library'
	id 'jacoco'
	id 'maven-publish'
	alias(libs.plugins.axion.release)
	alias(libs.plugins.sonarqube)
	alias(libs.plugins.springframework.boot) apply false
	alias(libs.plugins.lombok)
}

group = 'com.raketeneinhorn.testcontainers'
version = scmVersion.version

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

dependencies {
	implementation platform(SpringBootPlugin.BOM_COORDINATES)

	implementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.springframework.boot:spring-boot-testcontainers'
	implementation 'org.testcontainers:junit-jupiter'
	implementation 'org.testcontainers:postgresql'
	implementation libs.dasniko.testcontainers.keycloak

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-web'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
	}
}

sonar {
	properties {
		property "sonar.projectKey", "raketeneinhorn_raketeneinhorn-testcontainers"
		property "sonar.organization", "raketeneinhorn"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

publishing {
	publications {
		raketeneinhornTestcontainers(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/raketeneinhorn/raketeneinhorn-testcontainers"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}
